apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.dev.base"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //指定EventBus加速用的Index
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: "com.dev.base.MyEventBusIndex"]
            }
        }
    }
    signingConfigs {
        config {
            //签名文件在项目根目录下
            //KEY_ALIAS，KEY_PASSWORD，STORE_PASSWORD写在项目根目录下的gradle.properties里
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storeFile file('../test.jks')
            storePassword STORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config//使用上面配置的签名进行打包
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.config//debug模式下也使用上面配置的签名进行打包
        }
    }

    //greendao配置
    greendao {
        //数据库版本号，升级时进行修改
        schemaVersion 1
        daoPackage 'com.dev.base.mvp.model.db.greendao' //生成的DAO，DaoMaster和DaoSession的包名。默认是实体的包名
        targetGenDir 'src/main/java' //生成源文件的路径。默认源文件目录是在build目录中的(build/generated/source/greendao)
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    api 'com.android.support:appcompat-v7:27.0.2'
    api 'com.android.support:design:27.0.2'
    api 'com.android.support:recyclerview-v7:27.0.2'
    api 'com.android.support:cardview-v7:27.0.2'
    //LeakCanary内存泄漏检测
//    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
//    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //依赖Devring库
    //该库中已添加了Dagger2，Retrofit2，RxJava2，RxAndroid2，RxLifeCycle2，RxPermission2的依赖，不必重复添加。
    api(project(path: ':devring')) {
        //exclude module: 'glide' //如果不使用图片加载模块或者该模块不使用glide实现，那么可以加上这句移除Devring库中对glide的依赖以减小包体积
        //exclude module: 'eventbus' //如果不使用事件总线模块或者该模块不使用eventbus实现，那么可以加上这句移除Devring库中对eventbus的依赖以减小包体积
        //exclude module: 'greendao' //如果不使用数据库模块或者该模块不使用greendao实现，那么可以加上这句移除Devring库中对greendao的依赖以减小包体积
    }

    //══════════════════════资源绑定(ButterKnife)开始══════════════════════
    api 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'//用于butterknife在编译时根据注解生成相关文件
    //══════════════════════资源绑定(ButterKnife)结束══════════════════════

    //══════════════════════依赖注入(Dagger2)开始══════════════════════
    //如果你项目需要使用到dagger2，那就需要添加注解处理器的依赖
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'//用于dagger2在编译时根据注解生成相关文件
    //══════════════════════依赖注入(Dagger2)结束══════════════════════

    //══════════════════════图片加载开始══════════════════════
    //Fresco
    //为了演示如何使用Fresco替换掉DevRing中默认的Glide，需添加Fresco的相关依赖
    //Fresco体积较大，如果对图片加载要求不高，建议使用Glide
    api 'com.facebook.fresco:fresco:1.5.0'
    api 'com.facebook.fresco:animated-gif:1.5.0' //用于Fresco加载gif动图
    api 'com.facebook.fresco:imagepipeline-okhttp3:1.3.0'//用于替换fresco的网络加载为okhttp
    api 'jp.wasabeef:fresco-processors:2.1.0@aar'//用于提供fresco的各种图片变换
    //══════════════════════图片加载结束══════════════════════

    //══════════════════════数据库(GreenDao)开始══════════════════════
//    api 'net.zetetic:android-database-sqlcipher:3.5.7'//使用加密数据库时需要添加，使用后会apk会变大很多，如果对安全性要求不高，不建议使用
    //══════════════════════数据库(GreenDao)结束══════════════════════

    //══════════════════════事件总线(EventBus)开始══════════════════════
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'//用于eventbus在编译时生成加速用的Index文件
    //══════════════════════事件总线(EventBus)结束══════════════════════
}

